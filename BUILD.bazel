load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//:eslint/package_json.bzl", eslint_bin = "bin")
load("@npm//:vite/package_json.bzl", vite_bin = "bin")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load(":defs.bzl", "type_check")

npm_link_all_packages()

filegroup(
    name = "sources",
    srcs = glob([
        "src/**",
    ]) + [
        "index.html",
        ":node_modules/@types/react",
        ":node_modules/@types/react-dom",
        ":node_modules/react",
        ":node_modules/react-dom",
    ],
)

filegroup(
    name = "configuration",
    srcs = glob([
        "tsconfig.*",
    ]) + [
        "vite.config.ts",
        ":node_modules/@types/node",
        ":node_modules/@vitejs/plugin-react-swc",
        ":node_modules/vite",
    ],
)

filegroup(
    name = "assets",
    srcs = glob([
        "public/**",
    ]),
)

filegroup(
    name = "files",
    srcs = glob([
        "tsconfig.*",
    ]) + [
        "package.json",
        ":assets",
        ":configuration",
        ":sources",
    ],
)

write_file(
    name = "chdir",
    out = "chdir.cjs",
    content = [
        "process.chdir(require('path').join(process.cwd(), '../../..'));",
    ],
)

vite_bin.vite_binary(
    name = "dev",
    args = [
        "--node_options=--require=./$(rootpath :chdir)",
    ],
    data = [
        ":chdir",
        ":files",
    ],
)

vite_bin.vite(
    name = "build",
    srcs = [
        ":files",
    ],
    args = [
        "build",
    ],
    chdir = package_name(),
    out_dirs = ["dist"],
)

vite_bin.vite_binary(
    name = "preview",
    args = [
        "preview",
        "--outDir",
        "$(rootpath :build)",
    ],
    data = [":build"],
)

type_check(
    name = "app",
    srcs = [
        ":node_modules/vite",
        ":sources",
    ],
    tsconfig = "tsconfig.app.json",
)

type_check(
    name = "node",
    srcs = [
        ":configuration",
    ],
    tsconfig = "tsconfig.node.json",
)

eslint_bin.eslint_test(
    name = "eslint",
    chdir = package_name(),
    data = [
        "eslint.config.mjs",
        ":files",
        "//:node_modules/@eslint/js",
        "//:node_modules/eslint-plugin-react-hooks",
        "//:node_modules/eslint-plugin-react-refresh",
        "//:node_modules/globals",
        "//:node_modules/typescript-eslint",
    ],
)

pkg_tar(
    name = "dist",
    srcs = [":build"],
    extension = "tar",
    package_dir = "/srv/http",
    strip_prefix = "dist",
)

pkg_tar(
    name = "header_config",
    srcs = ["headerConfig.json"],
    extension = "tar",
    package_dir = "/config",
)

oci_image(
    name = "image",
    base = "@gostatic",
    tars = [
        ":dist",
        ":header_config",
    ],
)

oci_load(
    name = "load",
    image = ":image",
    repo_tags = ["react-typescript-vite:latest"],
)
