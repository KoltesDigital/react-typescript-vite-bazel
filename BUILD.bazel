load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//:eslint/package_json.bzl", eslint_bin = "bin")
load("@npm//:vite/package_json.bzl", vite_bin = "bin")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load(":defs.bzl", "type_check")

npm_link_all_packages()

vite_bin.vite_binary(
    name = "dev",
    args = [
        "../../..",
    ],
)

vite_bin.vite(
    name = "build",
    srcs = glob([
        "public/**",
        "src/**",
    ]) + [
        "index.html",
        "vite.config.ts",
        ":node_modules/@vitejs/plugin-react-swc",
        ":node_modules/react",
        ":node_modules/react-dom",
        ":node_modules/vite",
    ],
    args = [
        "build",
    ],
    out_dirs = ["dist"],
)

vite_bin.vite_binary(
    name = "preview",
    args = [
        "preview",
        "--outDir",
        "$(rootpath :build)",
    ],
    data = [":build"],
)

pkg_tar(
    name = "dist",
    srcs = [":build"],
    extension = "tar",
    package_dir = "/srv/http",
    strip_prefix = "dist",
)

pkg_tar(
    name = "header_config",
    srcs = ["headerConfig.json"],
    extension = "tar",
    package_dir = "/config",
)

oci_image(
    name = "image",
    base = "@gostatic",
    tars = [
        ":dist",
        ":header_config",
    ],
)

oci_load(
    name = "load",
    image = ":image",
    repo_tags = ["react-typescript-vite:latest"],
)

eslint_bin.eslint_test(
    name = "eslint",
    data = glob([
        "src/**",
    ]) + [
        "eslint.config.mjs",
        ":node_modules/@eslint/js",
        ":node_modules/eslint-plugin-react-hooks",
        ":node_modules/eslint-plugin-react-refresh",
        ":node_modules/globals",
        ":node_modules/typescript-eslint",
    ],
)

type_check(
    name = "app",
    srcs = glob([
        "src/**/*.ts",
        "src/**/*.tsx",
    ]) + [
        ":node_modules/@types/react",
        ":node_modules/@types/react-dom",
        ":node_modules/vite",
    ],
    tsconfig = "tsconfig.app.json",
)

type_check(
    name = "node",
    srcs = glob([
        "*.ts",
    ]) + [
        ":node_modules/@vitejs/plugin-react-swc",
        ":node_modules/vite",
    ],
    tsconfig = "tsconfig.node.json",
)
